// >>> OpenAPI (1)
buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.4"
  }
}
// <<< OpenAPI (1)

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'

	// >>> Sonarqube (1)
	id "org.sonarqube" version "2.6.2"
	// <<< Sonarqube (1)
}

apply plugin: 'io.spring.dependency-management'

// >>> OpenAPI (2)
apply plugin: 'org.openapi.generator'
// <<< OpenAPI (2) 

// >>> Sonarqube (2)
task getVersion {
	doLast {
        println version
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Inventory Service 40 Gradle SonarQube Scanner' + ' [' + getVersion() + ']'
    }
}
// <<< Sonarqube (2)

group = 'com.redhat.cloudnative'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	// >>> OpenAPI (3)
	// https://mvnrepository.com/artifact/io.swagger/swagger-core
	compile group: 'io.swagger', name: 'swagger-core', version: '1.5.0'
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'
	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
	// <<< OpenAPI (3)

	// >>> Prometheus Metrics (1)
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-actuator-autoconfigure
	compile group: 'org.springframework.boot', name: 'spring-boot-actuator-autoconfigure', version: '2.1.3.RELEASE'
	// https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-prometheus
	compile group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.1.3'
	// <<< Prometheus Metrics (1)

	// >>> Database related (1)
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	runtimeOnly('com.h2database:h2')
	runtimeOnly('org.postgresql:postgresql')
	// <<< Database related (1)

	// >>> Configuration (1)
	// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-kubernetes-dependencies
	compile group: 'org.springframework.cloud', name: 'spring-cloud-kubernetes-dependencies', version: '0.2.0.RELEASE', ext: 'pom'
	// <<< Configuration (1)
}

// >>> OpenAPI (4)
openApiGenerate{
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/inventory.yaml".toString()
	outputDir = "$rootDir".toString() 
	apiPackage = "com.redhat.cloudnative.inventory.api"
	invokerPackage = "com.redhat.cloudnative.inventory"
	modelPackage = "com.redhat.cloudnative.inventory.model" 
	modelFilesConstrainedTo = []
	configOptions = [
		dateLibrary: "java8"
	]
}
// <<< OpenAPI (4)